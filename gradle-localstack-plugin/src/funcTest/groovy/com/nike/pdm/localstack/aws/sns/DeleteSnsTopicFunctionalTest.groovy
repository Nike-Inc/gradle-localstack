/*
 * Copyright 2020-present, Nike, Inc.
 * All rights reserved.
 *
 * This source code is licensed under the Apache-2.0 license found in
 * the LICENSE file in the root directory of this source tree.
 */
package com.nike.pdm.localstack.aws.sns

import com.nike.pdm.localstack.LocalStackDockerTestUtil
import com.nike.pdm.localstack.util.ComposeFile
import org.gradle.testkit.runner.GradleRunner
import org.junit.Rule
import org.junit.rules.TemporaryFolder
import spock.lang.Specification
import spock.lang.Timeout

import java.util.concurrent.TimeUnit

import static org.gradle.testkit.runner.TaskOutcome.SUCCESS

@Timeout(value = 3, unit = TimeUnit.MINUTES)
class DeleteSnsTopicFunctionalTest extends Specification {

    @Rule TemporaryFolder testProjectDir = new TemporaryFolder()
    File buildFile
    File composeFile

    LocalStackDockerTestUtil dockerTestUtil = new LocalStackDockerTestUtil()

    def setup() {
        buildFile = testProjectDir.newFile('build.gradle')

        testProjectDir.newFolder('localstack')
        composeFile = testProjectDir.newFile('localstack/localstack-docker-compose.yml')
    }

    def cleanup() {
        dockerTestUtil.killLocalStack()
    }

    def "should delete sns topic"() {
        given:
        buildFile << """
            import com.nike.pdm.localstack.aws.sns.CreateSnsTopicWithSqsEndpointTask

            plugins {
                id "java"
                id "org.springframework.boot"           version "2.2.4.RELEASE"
                id "io.spring.dependency-management"    version "1.0.9.RELEASE"
                id "com.avast.gradle.docker-compose"    version "0.12.1"
            }
            
            apply plugin: "com.nike.pdm.localstack"
            
            dockerCompose {
                useComposeFiles = [ 'localstack/localstack-docker-compose.yml' ]
            }
            
            task setupLocalTopic(type: CreateSnsTopicWithSqsEndpointTask) {
                topicName = 'catalog-product-drops'
            }
        """

        composeFile << ComposeFile.getContents()

        when:
        def result = GradleRunner.create()
                .withProjectDir(testProjectDir.root)
                .withArguments('startLocalStack', 'deleteSnsTopic', '--topicName=catalog-product-drops')
                .withPluginClasspath()
                .build()

        then:
        result.task(":deleteSnsTopic").outcome == SUCCESS
    }

    def "should return error for invalid topic"() {
        given:
        buildFile << """
            import com.nike.pdm.localstack.aws.sns.CreateSnsTopicWithSqsEndpointTask

            plugins {
                id "java"
                id "org.springframework.boot"           version "2.2.4.RELEASE"
                id "io.spring.dependency-management"    version "1.0.9.RELEASE"
                id "com.avast.gradle.docker-compose"    version "0.12.1"
            }
            
            apply plugin: "com.nike.pdm.localstack"
            
            dockerCompose {
                useComposeFiles = [ 'localstack/localstack-docker-compose.yml' ]
            }
            
            task setupLocalTopic(type: CreateSnsTopicWithSqsEndpointTask) {
                topicName = 'catalog-product-drops'
            }
        """

        composeFile << ComposeFile.getContents()

        when:
        def result = GradleRunner.create()
                .withProjectDir(testProjectDir.root)
                .withArguments('startLocalStack', 'deleteSnsTopic', '--topicName=invalid-topic')
                .withPluginClasspath()
                .build()

        then:
        result.task(":deleteSnsTopic").outcome == SUCCESS
        result.output.contains("Topic Not Found!")
    }
}
